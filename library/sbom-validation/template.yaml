apiVersion: templates.gatekeeper.sh/v1beta1
kind: ConstraintTemplate
metadata:
  name: sbomvalidation
spec:
  crd:
    spec:
      names:
        kind: SbomValidation
  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package sbomvalidation

        # Get data from Ratify
        remote_data := response {
          images := [img | img = input.review.object.spec.containers[_].image]
          response := external_data({"provider": "ratify-provider", "keys": images})
        }

        # Base Gatekeeper violation
        violation[{"msg": msg}] {
          general_violation[{"result": msg}]
        }
        
        # Check if there are any system errors
        general_violation[{"result": result}] {
          err := remote_data.system_error
          err != ""
          result := sprintf("System error calling external data provider: %s", [err])
        }
        
        # Check if there are errors for any of the images
        general_violation[{"result": result}] {
          count(remote_data.errors) > 0
          result := sprintf("Error validating one or more images: %s", remote_data.errors)
        }
        
        # Check if the success criteria is true
        general_violation[{"result": result}] {
          subject_validation := remote_data.responses[_]
          subject_validation[1].isSuccess == false
          result := sprintf("Subject failed verification: %s", [subject_validation[0]])
        }
        
        # Check for failed sbom validation
        general_violation[{"result": result}] {
          subject_results := remote_data.responses[_]
          subject_result := subject_results[1]
          sbom_results := [res | subject_result.verifierReports[i].name == "sbom"; res := subject_result.verifierReports[i]]
          sbom_result := sbom_results[_]
          sbom_result.isSuccess == false
          result = sprintf("Subject %s failed sbom validation: %s", [subject_results[0], sbom_result.message])
        }
